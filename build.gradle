plugins {
	id "java"
	id "application"
}

apply plugin: 'java'
apply plugin: 'application'

// this var for application plugin run task
mainClassName = "tempbot.Bot"

repositories {
	mavenCentral()
}

def lombokVersion = "org.projectlombok:lombok:1.18.30"

dependencies {
	compileOnly(lombokVersion)
	annotationProcessor(lombokVersion)
	implementation("net.dv8tion:JDA:5.0.1") {
		exclude module: 'opus-java'
	}
	implementation("org.snakeyaml:snakeyaml-engine:2.7+")
	implementation("org.tinylog:tinylog-api:2.7.+")
	implementation("org.tinylog:tinylog-impl:2.7.+")
	implementation("org.tinylog:slf4j-tinylog:2.7.+")

	// TODO: update to junit jupiter (v5)
	testImplementation("junit:junit-dep:4.11")
	testImplementation("org.hamcrest:hamcrest:2.2")
	testCompileOnly(lombokVersion)
	testAnnotationProcessor(lombokVersion)
}

compileJava {
	sourceCompatibility = 21
	targetCompatibility = 21
}

test {
	testLogging {
		// always run all tests even when nothing's changed
		outputs.upToDateWhen { false }
		// actually show messages from tests (sigh)
		showStandardStreams = true

		// TODO: make this nicer
		// print summary stats of test runs
		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				println "tests passed: ${result.successfulTestCount}"
				println "tests failed: ${result.failedTestCount}"
				if (result.skippedTestCount > 0) {
					println "tests skipped: ${result.skippedTestCount}"
				}
			}
		}
	}
}

jar {
	destinationDirectory = file("$rootDir")
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	manifest {
		attributes "Main-Class": mainClassName
		attributes "Multi-Release": true
	}

	from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
