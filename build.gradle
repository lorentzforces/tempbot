plugins {
	id "java"
	id "application"
}

apply plugin: 'java'
apply plugin: 'application'

// this var for application plugin run task
mainClassName = "tempbot.Bot"

repositories {
	mavenCentral()
}

dependencies {
	// TODO(current): bump discord4j version to 3.2.2
	implementation "com.discord4j:discord4j-core:3.2.2"
	implementation "org.snakeyaml:snakeyaml-engine:2.3+"
	implementation "org.tinylog:tinylog-api:2.3.+"
	implementation "org.tinylog:tinylog-impl:2.3.+"
	implementation "org.tinylog:slf4j-tinylog:2.3.+"
	testImplementation "junit:junit-dep:4.11"
}

// TODO(current): bump to Java 17
compileJava {
	sourceCompatibility = 17
	targetCompatibility = 17
}

test {
	testLogging {
		// always run all tests even when nothing's changed
		outputs.upToDateWhen { false }
		// actually show messages from tests (sigh)
		showStandardStreams = true

		// TODO: make this nicer
		// print summary stats of test runs
		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				println "tests passed: ${result.successfulTestCount}"
				println "tests failed: ${result.failedTestCount}"
				if (result.skippedTestCount > 0) {
					println "tests skipped: ${result.skippedTestCount}"
				}
			}
		}
	}
}

jar {
	destinationDirectory = file("$rootDir")
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	manifest {
		attributes "Main-Class": mainClassName
		attributes "Multi-Release": true
	}

	from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
